import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath "com.github.samueltbrown:gradle-cucumber-plugin:0.8"

		// if you use the version of cucumber-jvm that the plugin was built with
		classpath "info.cukes:cucumber-core:1.2.2"
	}
}

ext {
	groovyVersion = '2.4.5'
	gebVersion = '0.13.1'
	seleniumVersion = '2.53.1'
	cucumberJvmVersion = '1.2.2'
	chromeDriverVersion = '2.19'
	phantomJsVersion = '1.9.8'

	ciBuild = System.getenv('SNAP_CI')
}

apply plugin: "groovy"
apply plugin: "com.github.samueltbrown.cucumber"

apply from: "gradle/idea/idea.gradle"
apply from: "gradle/osSpecificDownloads.gradle"

repositories {
	mavenCentral()
}

dependencies {
	testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
	testCompile "org.gebish:geb-core:$gebVersion"
	testCompile "org.seleniumhq.selenium:selenium-api:$seleniumVersion"

	testCompile "info.cukes:cucumber-core:$cucumberJvmVersion"
	testCompile "info.cukes:cucumber-groovy:$cucumberJvmVersion"

	// this supplies some pre-built cucumber steps for driving Geb,
	// see https://github.com/tomdcc/geb-cucumber
	testCompile "io.jdev.geb:geb-cucumber:0.3"

	// pick whichever drivers you want. We test against most of them here.
	// see src/test/resources/GebConfig.groovy for how to switch between them
	cucumberRuntime "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
	cucumberRuntime "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
	// using a custom version of phantomjs driver for now as the original one does not support WebDriver > 2.43.1
	cucumberRuntime("com.codeborne:phantomjsdriver:1.2.1") {
		// phantomjs driver pulls in a different selenium version
		transitive = false
	}
}

cucumber {
	formats = [
		'pretty', // prints nice format out to the console
		'html:build/reports/cucumber', // html
		'junit:build/cucumber.xml' // junit format for integration with CI tool etc
	]
	// src/cucumber/resources is included automatically
	glueDirs = [
		"classpath:io.jdev.geb.cucumber.steps.groovy.en"
	]

    // if geb.feature.path given use the value as the feature directory
    // else default to src/cucumber/features
    featureDirs = []
    if(System.getProperty("geb.feature.path")){
        featureDirs << System.getProperty("geb.feature.path")
    }else{
        featureDirs = ["src/cucumber/features"]
    }
}

tasks.cucumber {
	dependsOn unzipChromeDriver, unzipPhantomJs

	def chromeDriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "chromedriver.exe" : "chromedriver"
	def phantomJsFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "phantomjs.exe" : "bin/phantomjs"

	jvmOptions.systemProperties([
		"webdriver.chrome.driver": new File(unzipChromeDriver.outputs.files.singleFile, chromeDriverFilename).absolutePath,
		"phantomjs.binary.path": new File(unzipPhantomJs.outputs.files.singleFile, phantomJsFilename).absolutePath,
		"geb.cucumber.step.packages": "pages",
		"geb.env": System.getProperty("geb.env")
	])
}


apply from: "gradle/ci.gradle"
